TERMINAL SYMBOLS: id, num, real, true, false, basic, if, else, while, do, break

* Note that the basic terminal corresponds to a basic data type: int, float, char, and bool

PRODUCTIONS:

program  -> block
block    -> { decls stmts }
decls    -> decls decl 
          | E
decl     -> type id
type     -> type [ num ] 
          | basic
stmts    -> stmts stmt 
           | E
stmt     -> loc = bool ;
          | if ( bool ) stmt
          | if ( bool ) stmt else stmt
          | while ( bool ) stmt
          | do stmt while ( bool ) ;
          | break ;
          | block
loc      -> loc [ bool ] 
          | id
bool     -> bool || join 
          | join
join     -> join && equality 
          | equality
equality -> equality == rel 
          | equality != rel 
          | rel
rel      -> expr < expr 
          | expr <= expr
          | expr >= expr
          | expr > expr
          | expr
expr     -> expr + term
          | expr - term
          | term
term     -> term * unary
          | term / unary
          | unary
unary    -> ! unary
          | - unary
          | factor
factor   -> ( bool )
          | loc
          | num
          | real
          | true
          | false   
          
          
EXAMPLE:

{
  int i; int j; float v; float x; float[100] a;
  while (true) {
    do i = i+1; while( a[i] < v);
    do j = j+1; while( a[j] > v);
    if( i >= j ) break;
    x = a[i]; a[i] = a[j]; a[j] = x;
  }
}                 
                                           